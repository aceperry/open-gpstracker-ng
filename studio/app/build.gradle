apply plugin: 'com.android.application'
apply plugin: 'com.vanniktech.android.junit.jacoco'
apply plugin: 'kotlin-android'

junitJacoco {
    jacocoVersion = '0.7.7.201606060606'
}

android {
    def buildNumber = System.getenv("BUILD_NUMBER") as Integer ?: 1
    compileSdkVersion 24
    buildToolsVersion "$rootProject.buildToolsVersion"

    dataBinding {
        enabled = true
    }

    defaultConfig {
        applicationId "nl.sogeti.android.gpstracker.ng"
        minSdkVersion 16
        targetSdkVersion 24
        versionCode buildNumber
        versionName "2.0.0-SNAPSHOT"
        buildConfigField "int", "BUILD_NUMBER", buildNumber as String
        buildConfigField "String", "GIT_COMMIT", "\"" + System.getenv("GIT_COMMIT") + "\"" as String ?: "\"Unknown\""
        archivesBaseName = "opengpstracker-ng-$versionName"
        vectorDrawables.useSupportLibrary = true
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    if (System.getenv().containsKey("storeFile")) {
        apply from: 'signing.gradle'
    }

    buildTypes {
        debug {
            testCoverageEnabled true
        }
        release {
            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    productFlavors {
        mock {
            buildConfigField "String", "CONFIG_AUTHORITY", "\"nl.sogeti.android.gpstracker.ng.mock.authority\""
            buildConfigField "String", "CONFIG_BROADCAST", "\"nl.sogeti.android.gpstracker.ng.mock.broadcast\""
            buildConfigField "Class", "CONFIG_SERVICE", "nl.sogeti.android.gpstracker.ng.util.MockServiceManager.class"
        }
        prod {
            buildConfigField "String", "CONFIG_AUTHORITY", "nl.sogeti.android.gpstracker.integration.ContentConstants.GPS_TRACKS_AUTHORITY"
            buildConfigField "String", "CONFIG_BROADCAST", "nl.sogeti.android.gpstracker.integration.ServiceConstants.LOGGING_STATE_CHANGED_ACTION"
            buildConfigField "Class", "CONFIG_SERVICE", "nl.sogeti.android.gpstracker.integration.ServiceManager.class"
        }
    }

    variantFilter { variant ->
        def names = variant.flavors*.name

        if (names.contains("mock") && variant.buildType.name == "release") {
            variant.ignore = true
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    dataBinding {
        enabled = true
    }

    kapt {
        generateStubs = true
    }

    lintOptions {
        disable 'InvalidPackage'
        abortOnError false
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }
}

configurations.all {
    resolutionStrategy {
        force "com.android.support:support-annotations:$rootProject.supportLibraryVersion"
        force "com.android.support:support-v4:$rootProject.supportLibraryVersion"
        force "com.android.support:design:$rootProject.supportLibraryVersion"
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    compile project(':integration')
//    wearApp project(':wear')

    compile 'com.jakewharton.timber:timber:4.1.2'
    compile "org.jetbrains.kotlin:kotlin-stdlib:$rootProject.kotlinVersion"
    compile "com.google.android.gms:play-services-maps:$rootProject.playServicesVersion"
    compile "com.android.support:support-v4:$rootProject.supportLibraryVersion"
    compile "com.android.support:design:$rootProject.supportLibraryVersion"
    compile "com.android.support:support-annotations:$rootProject.supportLibraryVersion"
    compile "com.android.support.constraint:constraint-layout:$rootProject.constraintsLibraryVersion"
    kapt "com.android.databinding:compiler:$rootProject.dataBindingVersion"

    testCompile "junit:junit:$rootProject.junitVersion"
    testCompile "org.mockito:mockito-core:$rootProject.mockitoVersion"
    testCompile "org.robolectric:robolectric:3.1.2"

    kapt "com.android.support.test:runner:$rootProject.testRunnerVersion"
    androidTestCompile "com.android.support.test:runner:$rootProject.testRunnerVersion"
    androidTestCompile "com.android.support.test:rules:$rootProject.testRunnerVersion"
    androidTestCompile "org.mockito:mockito-core:$rootProject.mockitoVersion"
    androidTestCompile "com.android.support.test.espresso:espresso-core:$espressoVersion"
    androidTestCompile "com.android.support.test.espresso:espresso-contrib:$espressoVersion"

}
